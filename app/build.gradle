apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "jp.mamori_i.app"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
        buildConfigField "String", "BLE_SSID", '"0000C019-0000-1000-8000-00805F9B34FB"'
        buildConfigField "String", "V2_CHARACTERISTIC_ID", '"D61F4F27-3D6B-4B04-9E46-C9D2EA617F62"'

        buildConfigField "int", "SERVICE_FOREGROUND_NOTIFICATION_ID", '771579'
        buildConfigField "String", "SERVICE_FOREGROUND_CHANNEL_ID", '"Trace Log Channel ID"'
        buildConfigField "String", "SERVICE_FOREGROUND_CHANNEL_NAME", '"Trace Log Channel Name"'

        buildConfigField "long", "MAX_QUEUE_TIME", '7000'
        buildConfigField "long", "CONNECTION_TIMEOUT", '6000'
        buildConfigField "long", "HEALTH_CHECK_INTERVAL", '900000'

        buildConfigField "boolean", "INFINITE_SCANNING", 'false'
        buildConfigField "long", "SCAN_DURATION", '10000' // スキャンの時間
        buildConfigField "long", "SCAN_INTERVAL", '60000' // スキャンの間隔
        buildConfigField "long", "BLACKLIST_DURATION", '45000' // 連続で接続しないようにブラックリストに入る時間
        buildConfigField "boolean", "INFINITE_ADVERTISING", 'true'
        buildConfigField "long", "ADVERTISING_DURATION", '180000'
        buildConfigField "long", "ADVERTISING_INTERVAL", '5000'
    }

    signingConfigs {
        debug {
            storeFile file('../keystore/MIJDev.keystore')
            storePassword 'MIJDev'
            keyAlias = 'MIJDev'
            keyPassword 'MIJDev'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
        }
        debugMinify {
            minifyEnabled true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            buildConfigField("Boolean", "IS_IGONORE_SSL_ERROR", "false")
            buildConfigField("String", "BASE_URL", '"https://api-dev.mamori-i.jp/"')
        }
        stg {
            applicationIdSuffix ".stg"
            buildConfigField("Boolean", "IS_IGONORE_SSL_ERROR", "false")
            buildConfigField("String", "BASE_URL", '"https://api-stg.mamori-i.jp/"')
        }
        prod {
            buildConfigField("Boolean", "IS_IGONORE_SSL_ERROR", "false")
            buildConfigField("String", "BASE_URL", '"https://api.mamori-i.jp/"')
        }
    }
}

ext {
    moshi_version = "1.9.2"
    retrofit_version = "2.3.0"
    okhttp_version = "3.12.1"
    koin_version = "2.0.1"

    room_version = "2.2.5"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'

    // TODO: 最新があるっぽいが一旦はベースのプロジェクトにバージョン合わせ
    // しかもサポート終了っぽいので、LiveDataやリアクティブストリームに変更したほうがいいかも
    implementation group: 'androidx.localbroadcastmanager', name: 'localbroadcastmanager', version: '1.0.0-alpha3'
    // TODO: BlueTraceがGSONでの変換しているので追加。データをJSONにしないと思うので、最終的には削除
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'pub.devrel:easypermissions:3.0.0'
    // TODO: BlueTraceのRoomの部分の動作確認のために追加
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // For Kotlin use kapt instead of annotationProcessor

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // optional - Guava support for Room, including Optional and ListenableFuture
    //implementation "androidx.room:room-guava:$room_version"


    // firebase
    implementation 'com.google.firebase:firebase-core:17.3.0'
    implementation 'com.google.firebase:firebase-firestore:21.4.2'
    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-auth:19.3.0'
    implementation 'com.google.firebase:firebase-config:19.1.3'
    implementation 'com.google.firebase:firebase-storage:19.1.1'

    // data
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // rx
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxbinding3:rxbinding-core:3.1.0"

    // di
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"
    androidTestImplementation "org.koin:koin-test:$koin_version"

    // Progress // TODO 仮
    implementation 'com.kaopiz:kprogresshud:1.2.0'

    // RecycleView
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Test Mock
    testImplementation 'io.mockk:mockk:1.10.0'
    androidTestImplementation 'io.mockk:mockk:1.10.0'
}
